apply plugin: 'clone-artifacts'
apply plugin: 'sdk-tools'

// artifact cloning destinations
cloneArtifacts {
    mainRepo = "$rootDir/../prebuilts/tools/common/m2/repository"
    secondaryRepo = "$rootDir/../prebuilts/tools/common/m2/internal"
}

// ext.androidHostOut is shared by all tools/{base,build,swt} gradle projects/
ext.androidHostOut = file("$rootDir/../out")
// rootProject.buildDir is specific to this gradle build.
buildDir = new File(ext.androidHostOut, "tools")

ext.localRepo = project.hasProperty('localRepo') ? localRepo : "$ext.androidHostOut/repo"

apply from: "$rootDir/buildSrc/base/version.gradle"

subprojects { Project project ->
    // Change buildDir first so that all plugins pick up the new value.
    project.buildDir = project.file("$project.parent.buildDir/$project.name")

    // only configure leaf projects.
    if (!project.getSubprojects().isEmpty()) return

    apply plugin: 'java'
    apply plugin: 'clone-artifacts'

    repositories {
        maven { url = uri(rootProject.cloneArtifacts.mainRepo) }
        maven { url = uri(rootProject.cloneArtifacts.secondaryRepo) }
    }

    test {
        // todo: disable this for CI since we'll use the CI UI to do the report.
        reports.html.enabled = true
    }

    apply from: "$rootDir/buildSrc/base/base.gradle"
}

buildDir = new File(ext.androidHostOut, "build")

// delay evaluation of this project before all subprojects have been evaluated.
subprojects.each { subproject -> evaluationDependsOn(subproject.path) }

def testTasks = subprojects.collect { it.tasks.withType(Test) }.flatten()

task aggregateResults(type: Copy) {
    from { testTasks*.testResultsDir }
    into { file("$buildDir/results") }
}

task prepareRepo(type: Copy) {
    from { rootProject.cloneArtifacts.mainRepo }
    from { rootProject.cloneArtifacts.secondaryRepo }
    into { "$rootProject.ext.androidHostOut/repo" }
}

task copyGradleProperty(type: Copy) {
    from { "${System.env.HOME}/.gradle/gradle.properties" }
    into { gradle.gradleUserHomeDir }
}

tasks.create(name: 'publishLocal')
subprojects { project ->
   if (project.tasks.findByName('publishLocal') != null) {
       rootProject.publishLocal.dependsOn project.publishLocal
   }
}

task setupGradleInIde << {
   File dir = gradle.gradleHomeDir
   String link = dir.path.substring(project.ext.androidHostOut.path.length() + 1)
   String command = "ln -s $link ${new File(project.ext.androidHostOut, "gradle-dist-link").path}"
   command.execute()
}
